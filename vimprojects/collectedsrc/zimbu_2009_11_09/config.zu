#
# The Zimbu compiler written in Zimbu
#
# Config class.
#
# Copyright 2009 Bram Moolenaar
# Licensed under the Apache License, Version 2.0.  See the LICENSE file or
# obtain a copy at: http://www.apache.org/licenses/LICENSE-2.0
#

#
# Figures out properties of the compiler and libraries.
#
# TODO: this should be run as a separate configure program, before building
# Zimbu from C code, with "make bootstrap".
#

MODULE Config
  string   int64name   # C type name for 64 bit int
  string   int32name   # C type name for 32 bit int
  int      intSize     # C sizeof(int)

  list<string> libArgs = NEW()  # extra compiler arguments
  string   exeSuffix            # suffix used for an executable
  string   compiler = "cc"
  string   threadArg            # extra compiler argument for threads

  string libPath                # path to "lib" directory
  string zudirName = "ZUDIR"

  FUNC status run()
    int64name = "long long"
    int32name = "int"
    int sz
>>>
  Vsz = sizeof(int);
<<<
    intSize = sz

    string suf = ""
    string targ = "-lpthread"
>>>
#if defined(__MINGW32__) || defined(_MSC_VER)
  Vsuf = ".exe";  /* set to ".exe" for MS-Windows */
#endif
#if defined(__MINGW32__)
  Vtarg = "-lpthreadGC2";  /* suggested by edcdave, requires pthreads-win32 */
#endif
<<<
    exeSuffix = suf
    threadArg = targ

    # Set the libPath relative to the compiler executable.
    int i
    string root
    i = ARG.name.rindex('/')
    IF i < 0
      i = ARG.name.rindex('\\')
    }
    IF i < 0
      root = ""
    ELSE
      root = ARG.name.slice(0, i)
    }
    libPath = root .. "lib"

    RETURN OK
  }

  PROC addThreadLib()
    libArgs.add(threadArg)
  }

  FUNC string compilerCmd(string srcName, string binName)
    string cmd = compiler .. " -g -o \"" .. binName .. "\" "
    FOR l IN libArgs
      cmd ..= l .. " "
    }
    RETURN cmd .. "\"" .. srcName .. "\""
  }
}
