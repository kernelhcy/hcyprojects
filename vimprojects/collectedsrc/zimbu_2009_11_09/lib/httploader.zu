#
# The Zimbu compiler written in Zimbu
#
# HTTP module loader
#
# Copyright 2009 Bram Moolenaar
# Licensed under the Apache License, Version 2.0.  See the LICENSE file or
# obtain a copy at: http://www.apache.org/licenses/LICENSE-2.0
#

IMPORT "../builtin.zu"
IMPORT "../node.zu"
IMPORT "../scope.zu"
IMPORT "../symbol.zu"
IMPORT "../write_c.zu"

MODULE HTTPloader
  bool useServer
  Symbol moduleSymbol

  # Prepare for using the builtin HTTP module.
  FUNC Builtin prepare()
    Builtin b = NEW()
    b.moduleName = "HTTPmodule"
    b.fileName = "httpmodule.zu"
    b.setup = setup
    RETURN b
  }

  PROC setup(Builtin builtin)
    useServer = TRUE
    IF moduleSymbol == NIL
      # Add this module to the list of predefined symbols.
      moduleSymbol = NEW(Node.Type.lib_module)
      moduleSymbol.name = "HTTP"
      moduleSymbol.cName = "MHTTP"
      Scope.addPredefinedSymbol(moduleSymbol)

      # Take over the members of HTTPmodule
      builtin.addAllMembers(moduleSymbol)

      # Write headers later.
      Write_C.includeWriters.add(writeIncl)
    }
  }

  PROC writeIncl(Scope topScope, IO.File fd)
    IF useServer
      fd.write('''
#include <sys/types.h>
#include <sys/socket.h>
#include <netinet/in.h>
#include <arpa/inet.h>
''')
    }
  }

}
