# Zimbu example program: Run the HTTP server
#
# Copyright 2009 Bram Moolenaar
# Licensed under the Apache License, Version 2.0.  See the LICENSE file or
# obtain a copy at: http://www.apache.org/licenses/LICENSE-2.0

int tick

MAIN()
  HTTP.Server server = NEW(8888)
  # TODO: make this configurable
  server.fileRoot = "/home/mool/zimbu/pages"

  HTTP.FileServlet fs = NEW("index.html")
  fs.addPath("/")
  fs.addPath("/index.html")
  fs.addPath("/index.htm")
  server.addServlet(fs)

  HTTP.StringServlet ss = NEW("This is zimbu.html")
  ss.addPath("/zimbu.html")
  server.addServlet(ss)

  FUNC string varzPage(HTTP.Request req, HTTP.Response resp)
    string page = "<html><body>"
    page += "tick: " .. tick
    page += "</body></html>"
    RETURN page
  }

  HTTP.FunctionServlet fcs = NEW(varzPage)
  fcs.addPath("/varz")
  server.addServlet(fcs)

  server.start()
  IO.writeLine("Server started")

  WHILE TRUE  # TODO: server.running()
    SYS.sleepSec(1)
    tick++
    IO.write(" " .. tick)
    IO.flush()
  }

}
